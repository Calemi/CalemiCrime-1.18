buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}"
group = 'com.tm.' + mod_id
archivesBaseName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {

    mappings channel: 'parchment', version: "1.18.1-2022.03.06-1.18.2"

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                calemicrime {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                calemicrime {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                calemicrime {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


repositories {

    maven {
        name = "JEI Maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }

    maven {
        name = "JEI Maven Fallback"
        url = "https://modmaven.dev"
    }

    maven {
        name = "Curios Maven"
        url = "https://maven.theillusivec4.top/"
    }

    maven {
        name = "Calemi Core Maven"
        url = "https://www.cursemaven.com"
    }
}

dependencies {

    minecraft 'net.minecraftforge:forge:' + minecraft_version + '-' + forge_version

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${minecraft_version}-${curios_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${minecraft_version}-${curios_version}:api")

    implementation fg.deobf("curse.maven:calemicore-573646:${core_file_id}")
    implementation fg.deobf("curse.maven:calemieconomy-579403:${economy_file_id}")

    implementation fg.deobf("curse.maven:architectury-419699:${architectury_file_id}")
    implementation fg.deobf("curse.maven:ftb-library-404465:${ftb_library_file_id}")
    implementation fg.deobf("curse.maven:ftb-teams-404468:${ftb_teams_file_id}")

    implementation fg.deobf("curse.maven:mantle-74924:${mantle_file_id}")
    implementation fg.deobf("curse.maven:tinkers-74072:${tinkers_file_id}")

    implementation fg.deobf("curse.maven:flywheel-486392:${flywheel_file_id}")
    implementation fg.deobf("curse.maven:create-328085:${create_file_id}")

    implementation fg.deobf("curse.maven:framework-549225:${framework_file_id}")
    implementation fg.deobf("curse.maven:gunmod-289479:${gunmod_file_id}")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Calemi's Organized Crime",
                "Specification-Vendor"    : "Team Monumental",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "Team Monumental",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

curseforge {
    apiKey = project.findProperty('curseforge_apikey') ?: '0'
    project {
        id = curse_project_id
        changelog = file('changelog.txt')
        releaseType = 'release'
        addGameVersion "${minecraft_version}"
        relations {
            requiredDependency 'calemi-core'
            requiredDependency 'architectury-api'
            requiredDependency 'ftb-library-forge'
            requiredDependency 'ftb-teams-forge'
            requiredDependency 'mantle'
            requiredDependency 'tinkers-construct'
            requiredDependency 'flywheel'
            requiredDependency 'create'
            requiredDependency 'framework'
            requiredDependency 'mrcrayfishs-gun-mod'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
